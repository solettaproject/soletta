# This file is part of the Soletta (TM) Project
#
# Copyright (C) 2015 Intel Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

constant-server(constant/float:value=1.5,value_spec=min:0|max:100|step:2.1415)
response-count(int/accumulator:setup_value=0|2)
constant-client(converter/empty-to-float:output_value=5.4)
string(constant/string:value="1.5")
json-str(constant/string:value="{\"value\":1.5,\"min\":0,\"max\":100,\"step\":2.1415}")

json-blob(converter/json-object-to-blob)
blob-str(converter/blob-to-string)
blob-to-str(converter/blob-to-string)

server(http-server/float:path=/test,port=8080)

get-blob(http-client/blob:url="http://localhost:8080/test")
get-string(http-client/string:url="http://localhost:8080/test", accept="text/plain")
get-json(http-client/json:url="http://localhost:8080/test")
post(http-client/float:url="http://localhost:8080/test")

server-validator(test/float-validator:sequence="1.5 5.4")
string-compare(string/compare)
blob-compare(string/compare)
json-compare(string/compare)

constant-server OUT -> IN server
constant-server OUT -> GET get-string
constant-server OUT -> GET get-json
constant-server OUT -> GET get-blob

server OUT -> IN server-validator

string OUT -> IN[0] string-compare
get-string OUT -> IN[1] string-compare

string OUT -> IN[0] blob-compare
get-blob OUT -> IN blob-to-str OUT -> IN[1] blob-compare

json-str OUT -> IN[0] json-compare
get-json OBJECT -> IN json-blob OUT -> IN blob-str
blob-str OUT -> IN[1] json-compare

get-blob OUT -> INC response-count
get-string OUT -> INC response-count
get-json OBJECT -> INC response-count
response-count OVERFLOW -> IN constant-client
constant-client OUT -> POST post

server-validator OUT -> RESULT test_server_output(test/result:timeout=6000)
string-compare EQUAL -> RESULT test_string_output(test/result:timeout=6000)
blob-compare EQUAL -> RESULT test_blob_output(test/result:timeout=6000)
json-compare EQUAL -> RESULT test_json_output(test/result:timeout=6000)
