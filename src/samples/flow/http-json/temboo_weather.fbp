# This file is part of the Soletta (TM) Project
#
# Copyright (C) 2015 Intel Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

DECLARE=UrlParam:composed-new:KEY(string)|VALUE(string)
DECLARE=ArrayParam:composed-new:KEY(string)|VALUE(json-array)

INPORT=Username.IN[0]:USERNAME
INPORT=url.USER:APP_NAME
INPORT=url.PASSWORD:APPID
INPORT=value.VALUE:LOCATION
OUTPORT=temp_avg.CELSIUS:TEMP_AVG
OUTPORT=temp_low.CELSIUS:TEMP_LOW
OUTPORT=temp_high.CELSIUS:TEMP_HIGH

#Build url and request parameters
url(http-client/create-url:base_uri="https://temboolive.com/temboo-api/1.0/choreos/Library/Labs/GetWeather/ByAddress")
Username(switcher/string) OUT[0] -> IN[0] server(string/concatenate)
_(constant/string:value=".temboolive.com") OUT -> IN[1] server

server OUT -> SERVER url
server OUT -> CREATE url

temboo_request(http-client/request:method="POST") ERROR -> IN error(console)
url OUT -> URL temboo_request

_(constant/string:value="x-temboo-domain") OUT -> KEY temboo_domain_param(UrlParam)
_(constant/string:value="/") OUT -> IN[0] temboo_domain(string/concatenate)
Username OUT[0] -> IN[1] temboo_domain
_(constant/string:value="/master") OUT -> IN[2] temboo_domain
temboo_domain OUT -> VALUE temboo_domain_param
temboo_domain_param OUT -> PARAM_HEADER temboo_request

_(constant/string:value="Content-Type") OUT -> KEY content_type_param(UrlParam)
_(constant/string:value="application/json") OUT -> VALUE content_type_param
content_type_param OUT -> PARAM_HEADER temboo_request

#Create request JSON
json_input(json/create-object)
name(UrlParam) OUT -> STRING json_input
_(constant/string:value="name") OUT -> KEY name
_(constant/string:value="Address") OUT -> VALUE name
value(UrlParam) OUT -> STRING json_input
_(constant/string:value="value") OUT -> KEY value
value OUT -> CREATE json_input
json_input OUT -> OBJECT inputs_array(json/create-array)
json_input OUT -> CREATE inputs_array
_(constant/string:value="inputs") OUT -> KEY inputs_field(ArrayParam)
inputs_array OUT -> VALUE inputs_field
inputs_field OUT -> ARRAY json_request(json/create-object)
inputs_field OUT -> CREATE json_request
json_request OUT -> IN blob(converter/json-object-to-blob) OUT -> CONTENT temboo_request

#Trigger request
blob OUT -> TRIGGER temboo_request

#Parse results
temboo_request OUT -> IN _(http-client/get-content) OUT -> IN result(converter/blob-to-json-object)

result OUT -> IN _(json/object-get-path:path="$.output.Response") STRING -> IN temboo_content(converter/string-to-json-object)
temboo_content OUT -> IN _(json/object-get-path:path="$.temperature") STRING -> IN _(converter/string-to-float) OUT -> FAHRENHEIT temp_avg(temperature/converter)
temboo_content OUT -> IN _(json/object-get-path:path="$.low") STRING -> IN _(converter/string-to-float) OUT -> FAHRENHEIT temp_low(temperature/converter)
temboo_content OUT -> IN _(json/object-get-path:path="$.high") STRING -> IN _(converter/string-to-float) OUT -> FAHRENHEIT temp_high(temperature/converter)
