# This file is part of the Soletta (TM) Project
#
# Copyright (C) 2015 Intel Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

DECLARE=UrlParam:composed-new:KEY(string)|VALUE(string)

INPORT=param_appid.VALUE:APPID
INPORT=param_location.VALUE:LOCATION
OUTPORT=temp.CELSIUS:TEMP_AVG
OUTPORT=temp_low.CELSIUS:TEMP_LOW
OUTPORT=temp_high.CELSIUS:TEMP_HIGH

#build URL
url(http-client/create-url:base_uri="http://api.openweathermap.org/data/2.5/weather")
param_appid(UrlParam) OUT -> PARAM url
param_location(UrlParam) OUT -> PARAM url
param_location OUT -> CREATE url

_(constant/string:value="appid") OUT -> KEY param_appid
_(constant/string:value="q") OUT -> KEY param_location

#Make request
url OUT -> URL result(http-client/json)
url OUT -> GET result

#Parse results
result OBJECT -> IN _(json/object-get-path:path="$.main.temp") FLOAT -> KELVIN temp(temperature/converter)
result OBJECT -> IN _(json/object-get-path:path="$.main.temp_min") FLOAT -> KELVIN temp_low(temperature/converter)
result OBJECT -> IN _(json/object-get-path:path="$.main.temp_max") FLOAT -> KELVIN temp_high(temperature/converter)
