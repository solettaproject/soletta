#!/usr/bin/env sol-fbp-runner

# This file is part of the Soletta (TM) Project
#
# Copyright (C) 2015 Intel Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Maps temperature readings (here, fake ones) to led colours on a
# led-strip. The hotter the temperature, the more led will be red. The
# remaining ones will be blue.

# These three fake the output of a temperature sensor
timer(timer)
random(random/int)
temp(int/map:output_range=min:-40|max:100|step:1)

# These are the ones needed to our flow
# led_map maps the expected temperature readings, from -40 to 100, to
# a number up to 31, meaning how many leds will be red.
led_map(int/map:use_input_range=false,input_range=min:-40|max:100,output_range=min:0|max:31|step:1)
less(int/less)
counter(int/accumulator:setup_value=max:31)
blue(converter/empty-to-rgb:output_value=blue:200|green:0|red:0)
red(converter/empty-to-rgb:output_value=red:200|green:0|blue:0)
led_strip(led-strip/lpd8806)

temp OUT -> IN led_map
led_map OUT -> INC counter

led_map OUT -> IN[0] less
counter OUT -> IN _(int/filter:min=1,max=31) OUT -> IN[1] less
counter OUT -> PIXEL led_strip

less OUT -> IN _(boolean/filter) TRUE -> IN red OUT -> COLOR led_strip
less OUT -> IN _(boolean/filter) FALSE -> IN blue OUT -> COLOR led_strip

less OUT ->  INC counter

counter OVERFLOW -> FLUSH led_strip
counter OVERFLOW -> RESET counter
