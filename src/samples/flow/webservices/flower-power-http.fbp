#!/usr/bin/env sol-fbp-runner

# This file is part of the Soletta (TM) Project
#
# Copyright (C) 2015 Intel Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Usage: ./flower-power-http.fbp USERNAME PASSWORD PLANT_ID SENSOR_ID
#
# Register at https://apiflowerpower.parrot.com/api_access/signup
# to receive client_id and client_secret and use it on Flowers node

Username(app/argv:index=1)
Password(app/argv:index=2)
PlantID(app/argv:index=3)
SensorID(app/argv:index=4)

Timer(timer:interval=3000)

Flowers(flower-power/http-get:client_id="CLIENT_ID", client_secret="CLIENT_SECRET")
Filter(flower-power/filter-id)
Parser(flower-power/get-value)
FilterSensor(flower-power/filter-sensor-id)
ParserSensor(flower-power/get-sensor-value)

Username OUT -> USERNAME Flowers
Password OUT -> PASSWORD Flowers
PlantID OUT -> ID Filter
SensorID OUT -> ID FilterSensor

# Information about plants

Timer OUT -> GET Flowers OUT -> IN Filter OUT -> IN Parser

Parser ID -> IN Plant(console)
Parser TIMESTAMP -> IN Timestamp(console)
Parser FERTILIZER -> IN FertilizerLevel(console)
Parser FERTILIZER_MIN -> IN FertilizerMin(console)
Parser FERTILIZER_MAX -> IN FertilizerMax(console)
Parser LIGHT -> IN Sunlight(console)
Parser LIGHT_MIN -> IN SunlightMin(console)
Parser LIGHT_MAX -> IN SunlightMax(console)
Parser KELVIN -> IN Temperature(console:suffix=" Kelvin")
Parser KELVIN_MIN -> IN TemperatureMin(console:suffix=" Kelvin")
Parser KELVIN_MAX -> IN TemperatureMax(console:suffix=" Kelvin")
Parser WATER -> IN WaterLevel(console)
Parser WATER_MIN -> IN WaterLevelMin(console)
Parser WATER_MAX -> IN WaterLevelMax(console)

# Information about sensors

Flowers DEVICE_INFO -> IN FilterSensor OUT -> IN ParserSensor

ParserSensor ID -> IN Sensor(console)
ParserSensor TIMESTAMP -> IN SensorTimestamp(console)
ParserSensor BATTERY_END_OF_LIFE -> IN SensorBatteryEOL(console)
ParserSensor BATTERY_LEVEL -> IN SensorBatteryLevel(console)
