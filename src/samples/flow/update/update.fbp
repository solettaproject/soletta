#!/usr/bin/env sol-fbp-runner
# This file is part of the Soletta Project
#
# Copyright (C) 2015 Intel Corporation. All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
#
#   * Redistributions of source code must retain the above copyright
#     notice, this list of conditions and the following disclaimer.
#   * Redistributions in binary form must reproduce the above copyright
#     notice, this list of conditions and the following disclaimer in
#     the documentation and/or other materials provided with the
#     distribution.
#   * Neither the name of Intel Corporation nor the names of its
#     contributors may be used to endorse or promote products derived
#     from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
# "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
# LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
# A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
# OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
# LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
# DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

# Checks for update to Soletta app displaying progress of FETCH
# task.
# To perform the update, it is necessary that a update module is loaded
# as backend. Each backend may be affected by different compile time
# options or environment variables.
# If there's more than one module available for Soletta, Soletta will
# arbitrarily load one of them, or the one defined by SOL_UPDATE_MODULE
# environment variable.
# Linux Micro EFI Update module understands the following environment
# variables:
#   - SOL_UPDATE_META_URL which defines the URL that it will look for
#   update information.
#   - SOL_UPDATE_FETCH_URL which defines the URL to fetch update file.
#   - SOL_APP_FILE_PATH which defines application path.
# Get more help for this module on kbuild menuconfig at
# Core Library -> Update Options -> Soletta Linux Micro EFI Update

check(update/check)
fetch(update/fetch)
install(update/install)

fetch_progress_timer(timer:interval=10)
fetch_progress_timer OUT -> GET_PROGRESS fetch
_(constant/boolean:value=false) OUT -> ENABLED fetch_progress_timer

check ERROR -> IN error_check(console)
fetch ERROR -> IN error_fetch(console)
install ERROR -> IN error_install(console)

_(constant/int:value=0) OUT -> CHECK check

check VERSION -> IN print_version(console)
check SIZE -> IN print_size(console)
check NEED_UPDATE -> IN print_need_update(console)
check NEED_UPDATE -> PULSE_IF_TRUE _(converter/boolean-to-empty) OUT -> FETCH fetch
check NEED_UPDATE -> ENABLED fetch_progress_timer

fetch PROGRESS -> IN print_progress(console)
fetch SUCCESS -> IN print_fetch_success(console)
fetch SUCCESS -> PULSE_IF_TRUE _(converter/boolean-to-empty) OUT -> INSTALL install
fetch SUCCESS -> IN _(converter/empty-to-boolean:output_value=false) OUT -> ENABLED fetch_progress_timer

install SUCCESS -> IN print_success(console)
