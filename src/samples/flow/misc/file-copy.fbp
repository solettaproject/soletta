#!/usr/bin/env sol-fbp-runner

# This file is part of the Soletta (TM) Project
#
# Copyright (C) 2015 Intel Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# usage: ./file-copy.fbp path/to/src path/to/dest

# check if number of args are correct
_(constant/int:value=2) OUT -> IN[0] ExpectedArgs(int/equal)
Args(app/argc-argv) ARGC -> IN[1] ExpectedArgs
ExpectedArgs OUT -> IN ArgsCountOK(console)

SrcPath(app/argv:index=1) OUT -> PATH fr(file/reader)
SrcPath ERROR -> IN Error(console)
DstPath(app/argv:index=2) OUT -> PATH fw(file/writer:permissions=0644)
DstPath ERROR -> IN Error

_(timer:interval=500) OUT -> IN Timer(console)
fr OUT -> IN fw
fr ERROR -> IN Error
fw BUSY -> IN Busy(console)
fw ERROR -> IN Error
fw SIZE -> IN Size(console)
fw DONE -> IN Done(console)

# quit after copy is done or on error
Quit(app/quit)
fw SIZE -> IN[0] Success(int/equal)
fw DONE -> IN[1] Success

Success OUT -> PULSE_IF_TRUE _(converter/boolean-to-empty) OUT -> QUIT Quit
fw ERROR -> ERROR Quit
